include "TriggerLibs/NativeLib"

trigger gather;
trigger passiveHarvest;
trigger returning;
trigger idle;
trigger building;
trigger techlab;
trigger research;
trigger pause;
trigger resume;
trigger cancel;
trigger unloadSquads;
trigger power;
trigger ownerSwap;
trigger topbarCheck;
trigger rallyCopy;
trigger techUnlock;
trigger INI;
int rate;
int spendrate;
unit topbarUnit;
bool noTopBar;
int topbarCastingPanel;
int topbarPanel;
int topbarCommandPanel;
string[12] topbarAbils;

void OrderWorkerToGather (unit worker) { //reduced "order workers to gather" function
	// Variable Declarations
    unitgroup lv_resourceGroup;
    unit lv_indexResource;

    // Automatic Variable Declarations
    unitgroup auto9E4A1F0D_g;
    int auto9E4A1F0D_u;

    // Variable Initialization

    // Implementation
	lv_resourceGroup = UnitGroupEmpty();
	auto9E4A1F0D_g = UnitGroup(null, c_playerAny, RegionCircle(UnitGetPosition(worker), 9.0), UnitFilter((1 << c_targetFilterHarvestableResource), 0, (1 << c_targetFilterMissile), (1 << (c_targetFilterDead - 32)) | (1 << (c_targetFilterHidden - 32))), 0);
	auto9E4A1F0D_u = UnitGroupCount(auto9E4A1F0D_g, c_unitCountAll);
	for (;; auto9E4A1F0D_u -= 1) {
		lv_indexResource = UnitGroupUnitFromEnd(auto9E4A1F0D_g, auto9E4A1F0D_u);
		if (lv_indexResource == null) { break; }
		if ((!UnitHasBehavior(lv_indexResource, "Refinery")) && ((UnitGetOwner(lv_indexResource) == 0) || (UnitGetOwner(lv_indexResource) == UnitGetOwner(worker)))) {
			UnitGroupAdd(lv_resourceGroup, lv_indexResource);
		}
	}
	if ((UnitGroupCount(lv_resourceGroup, c_unitCountAlive) > 0)) {
		UnitIssueOrder(worker, OrderTargetingUnit(null, UnitGroupClosestToPoint(lv_resourceGroup, UnitGetPosition(worker))), c_orderQueueReplace);
	}
}

void buildingSwap (unit building) {
	if (UnitGetType(building) == "CommandCenter") {
		libNtve_gf_ReplaceUnit(building, "CommandCenter2", 1);
	} else if (UnitGetType(building) == "PlanetaryFortress") {
		libNtve_gf_ReplaceUnit(building, "PlanetaryFortress2", 1);
	} else if (UnitGetType(building) == "Barracks") {
		libNtve_gf_ReplaceUnit(building, "Barracks2", 1);
	} else if (UnitGetType(building) == "EngineeringBay") {
		libNtve_gf_ReplaceUnit(building, "EngineeringBay2", 1);
	} else if (UnitGetType(building) == "Factory") {
		libNtve_gf_ReplaceUnit(building, "Factory2", 1);
	} else if (UnitGetType(building) == "FactoryFlying") {
		libNtve_gf_ReplaceUnit(building, "Factory2Flying", 1);
	} else if (UnitGetType(building) == "Starport") {
		libNtve_gf_ReplaceUnit(building, "Starport2", 1);
	} else if (UnitGetType(building) == "Armory") {
		libNtve_gf_ReplaceUnit(building, "Armory2", 1);
	} else if (UnitGetType(building) == "FusionCore") {
		libNtve_gf_ReplaceUnit(building, "FusionCore2", 1);
	} else if (UnitGetType(building) == "MissileTurret") {
		libNtve_gf_ReplaceUnit(building, "MissileTurret2", 1);
	} else if (UnitGetType(building) == "GhostAcademy") {
		libNtve_gf_ReplaceUnit(building, "GhostAcademy2", 1);
	} else if (UnitGetType(building) == "SensorTower") {
		libNtve_gf_ReplaceUnit(building, "SensorTower2", 1);
	} else if (UnitGetType(building) == "Bunker") {
		libNtve_gf_ReplaceUnit(building, "Bunker2", 1);
	} else if (UnitGetType(building) == "SupplyDepot") {
		libNtve_gf_ReplaceUnit(building, "SupplyDepot2", 1);
		if (TechTreeUnitAliasCount(UnitGetOwner(libNtve_gf_LastReplacedUnit()), "Alias_EngineeringBay", c_techCountCompleteOnly) > 0) {
			UnitCreateEffectUnit(libNtve_gf_LastReplacedUnit(), "SupplyDropApplyTempBehavior", libNtve_gf_LastReplacedUnit());
		}
	} else if (UnitGetType(building) == "SCV") {
		libNtve_gf_ReplaceUnit(building, "SCV2", 1);
		OrderWorkerToGather(libNtve_gf_LastReplacedUnit());
	} else if (UnitGetType(building) == "SCV2") {
		OrderWorkerToGather(building);
	} else if (UnitGetType(building) == "MercCompound") {
		UnitRemove(building);
	}
}

void DisableTopBar () {
	noTopBar = true;
}

void AddTopbarAbil(string abil) {
	// maybe set cooldowns to global
	// needs reset, which means clear charges, re-add starting charges, then add 0 cooldown, with delays betweenf each.
	int lv_i;
	if (UnitAbilityExists(topbarUnit, abil)) {
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("exiting, already has ability: " +abil));
		return;
	}
	for (lv_i = 0; topbarAbils[lv_i] != null; lv_i += 1) {
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("Skipping "+ IntToString(lv_i) + ": " +topbarAbils[lv_i]+IntToString(lv_i+3)));
		if (lv_i == 11) {
			return;
		}
	}
	if (lv_i > 11) {
		return;
	}
	//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("Adding: " +abil+IntToString(lv_i+3)));
	UnitAbilityAdd(topbarUnit, abil);
	topbarAbils[lv_i] = abil;
	PlayerApplySkin(1, abil+IntToString(lv_i+3), true);
}

void RemoveTopbarAbil(string abil) {
	int lv_i;
	if (!UnitAbilityExists(topbarUnit, abil)) {
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("exiting, does not have: " +abil));
		//return;
	}
	for (lv_i = 0; topbarAbils[lv_i] != abil; lv_i += 1) {
		if (lv_i == 11) {
			UnitAbilityAdd(topbarUnit, abil);
			UnitAbilityRemove(topbarUnit, abil);
			//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("exiting, could not find: " +abil));
			return;
		}
	}
	if (lv_i > 11) {
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("exiting, what???: " +abil));
		return;
	}
	//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("Removing: " +abil+IntToString(lv_i+3)));
	topbarAbils[lv_i] = null;
	UnitAbilityAdd(topbarUnit, abil); //I have no idea why this changes anything
	UnitAbilityRemove(topbarUnit, abil);
	PlayerApplySkin(1, abil+IntToString(lv_i+3), false);
	lv_i += 1;
	for (; topbarAbils[lv_i] != null; lv_i += 1) {
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("Current: " +topbarAbils[lv_i]+IntToString(lv_i+3)));
		PlayerApplySkin(1, topbarAbils[lv_i]+IntToString(lv_i+3), false);
		PlayerApplySkin(1, topbarAbils[lv_i]+IntToString(lv_i+2), true);
		//UIDisplayMessage(PlayerGroupAll(), c_messageAreaDebug, StringToText("New: " +topbarAbils[lv_i]+IntToString(lv_i+2)));
		topbarAbils[lv_i - 1] = topbarAbils[lv_i];
		topbarAbils[lv_i] = null;
	}
}

void AddTopbars () {
	if (TechTreeUnitAliasCount(1, "Alias_ShadowOps", c_techCountCompleteOnly) > 0) {
		if (TechTreeUpgradeCount(1, "HireKelmorianMinersPH", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("InfantryMercenaries");
		}
		if (TechTreeUpgradeCount(1, "HireSiegeBreakersPH", c_techCountCompleteOnly) > 0 || TechTreeUpgradeCount(1, "HireSpartanCompanyPH", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("VehicleMercenaries");
		}
		if (TechTreeUpgradeCount(1, "HireHelsAngelsPH", c_techCountCompleteOnly) > 0 || TechTreeUpgradeCount(1, "HireDuskWingPH", c_techCountCompleteOnly) > 0 || TechTreeUpgradeCount(1, "HireDukesRevenge", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("StarshipMercenaries");
		}
		if (TechTreeUpgradeCount(1, "OrbitalRelay", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("TacticalMissileStrike");
		}
	}
	if (TechTreeUnitAliasCount(1, "Alias_FusionCore", c_techCountCompleteOnly) > 0) {
		AddTopbarAbil("HyperionYamato");
		if (TechTreeUpgradeCount(1, "HiveMindEmulator", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("HiveMindEmulator");
		}
		if (TechTreeUpgradeCount(1, "PsiDisruptor", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("PsiDisruptor");
		}
		if (TechTreeAbilityIsAllowed(1, AbilityCommand("DeployLaserDrill", 0))) {
			AddTopbarAbil("DeployLaserDrill");
		}
		if (TechTreeAbilityIsAllowed(1, AbilityCommand("ZergHypervirus", 0))) {
			AddTopbarAbil("ZergHypervirus");
		}
		if (TechTreeAbilityIsAllowed(1, AbilityCommand("CallInGoldenArmada", 0))) {
			AddTopbarAbil("CallInGoldenArmada");
		}
	}
	if (TechTreeUnitAliasCount(1, "Alias_EngineeringBay", c_techCountCompleteOnly) > 0) {
		if (TechTreeUpgradeCount(1, "OrbitalRelay", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("ScannerSweepTopbar");
		}
	}
	if (TechTreeUnitAliasCount(1, "Alias_Armory", c_techCountCompleteOnly) > 0) {
		if (TechTreeUpgradeCount(1, "OrbitalRelay", c_techCountCompleteOnly) > 0) {
			AddTopbarAbil("TacNukeStrikeTopbar");
		}
	}
}

void RemoveTopbars () {
	if (TechTreeUnitAliasCount(1, "Alias_ShadowOps", c_techCountCompleteOnly) == 0) {
		RemoveTopbarAbil("InfantryMercenaries");
		RemoveTopbarAbil("VehicleMercenaries");
		RemoveTopbarAbil("StarshipMercenaries");
		RemoveTopbarAbil("TacticalMissileStrike");
	}
	if (TechTreeUnitAliasCount(1, "Alias_FusionCore", c_techCountCompleteOnly) == 0) {
		RemoveTopbarAbil("HyperionYamato");
		RemoveTopbarAbil("DeployLaserDrill");
		RemoveTopbarAbil("HiveMindEmulator");
		RemoveTopbarAbil("PsiDisruptor");
		RemoveTopbarAbil("ZergHypervirus");
		RemoveTopbarAbil("CallInGoldenArmada");
	}
	if (TechTreeUnitAliasCount(1, "Alias_EngineeringBay", c_techCountCompleteOnly) == 0) {
		RemoveTopbarAbil("ScannerSweepTopbar");
	}
	if (TechTreeUnitAliasCount(1, "Alias_Armory", c_techCountCompleteOnly) == 0) {
		RemoveTopbarAbil("TacNukeStrikeTopbar");
	}
}

void RefreshTopbar () {
	AddTopbars();
	RemoveTopbars();
}

bool Gather_Func (bool testConds, bool runActions) {
	unit gatherUnit;
	unit refinery;
	int resourceAmount;
	fixed time;
	int resourceDelta;
	order lv_order;
	int lv_i;
	
	if (testConds) {
		if (UnitHasBehavior(EventUnit(), "DropOffStun")) {
			return false;
		}
		if (!UnitHasBehavior(EventUnitTargetUnit(), "Refinery")) {
			return false;
		}
	}
	
	if (!runActions) {
		return true;
	}
	UnitBehaviorAdd(EventUnit(), "DropOffStun", EventUnit(), 1);
	time = GameGetMissionTime();
	gatherUnit = EventUnit();
	refinery = EventUnitTargetUnit();
	resourceAmount = UnitGetPropertyInt(gatherUnit, c_unitPropCarriedMinerals, true);
	while (UnitIsAlive(gatherUnit) && resourceAmount > 0 && UnitIsAlive(refinery) && OrderGetAbilityCommand(UnitOrder(gatherUnit, 0)) == AbilityCommand("SCVHarvest", 0) && OrderGetTargetUnit(UnitOrder(gatherUnit, 0)) == refinery) {
		resourceDelta = RoundI((GameGetMissionTime() - time) * rate);
		time = GameGetMissionTime();
		PlayerModifyPropertyInt(UnitGetOwner(gatherUnit), c_playerPropMinerals, c_playerPropOperAdd, MinI(resourceAmount, resourceDelta));
		PlayerModifyPropertyInt(UnitGetOwner(gatherUnit), c_playerPropMineralsCollected, c_playerPropOperAdd, MinI(resourceAmount, resourceDelta));
		resourceAmount -= MinI(resourceAmount, resourceDelta);
		UnitSetPropertyInt(gatherUnit, c_unitPropCarriedMinerals, resourceAmount);
		Wait(0.0625, c_timeGame);
	}
	if (!UnitIsAlive(gatherUnit)) {
		return true;
		UnitBehaviorRemove(gatherUnit, "DropOffStun", 1);
	}
	if (UnitGetPropertyInt(gatherUnit, c_unitPropCarriedMinerals, true) == 0) {
		UnitBehaviorRemove(gatherUnit, "CarryMineralFieldMinerals", 1);
	}
	if (OrderGetAbilityCommand(UnitOrder(gatherUnit, 0)) == AbilityCommand("SCVHarvest", 0) && OrderGetTargetUnit(UnitOrder(gatherUnit, 0)) == refinery) {
		UnitIssueOrder(gatherUnit, Order(AbilityCommand("stop", 0)), c_orderQueueReplace);
		for (lv_i = 1; lv_i <= 4; lv_i += 1) {
			if (UnitRallyPointTargetPoint(refinery, 1, lv_i) != null && UnitRallyPointTargetUnit(refinery, 1, lv_i) != refinery)  {
				if (UnitIsValid(UnitRallyPointTargetUnit(refinery, 1, lv_i)) && UnitRallyPointTargetUnit(refinery, 1, lv_i) != null) {
					lv_order = OrderTargetingUnit(null, UnitRallyPointTargetUnit(refinery, 1, lv_i));
					OrderSetFlag(lv_order, c_cmdSmartRally, true);
					if (UnitFilterMatch(UnitRallyPointTargetUnit(refinery, 1, lv_i), 1, UnitFilterStr("HarvestableResource;Dead,Missile"))){
						UnitIssueOrder(gatherUnit, lv_order, c_orderQueueAddToEnd);
						break;
					}
				} else {
					lv_order = OrderTargetingPoint(AbilityCommand("move", 0), UnitRallyPointTargetPoint(refinery, 1, lv_i));
					OrderSetFlag(lv_order, c_cmdSmartRally, true);
				} 
			}
			else {
				lv_order = OrderTargetingUnit(AbilityCommand("SCVHarvest", 0), AIGetClosestUnit(gatherUnit, UnitGroup("RefineryBounce", c_playerAny, RegionEntireMap(), UnitFilter(0,0,0,0), 0), true));
				break;
			}
			UnitIssueOrder(gatherUnit, lv_order, c_orderQueueAddToEnd);
		}
	}
	UnitBehaviorRemove(gatherUnit, "DropOffStun", 1);
	AISetUnitScriptControlled(gatherUnit, false);
	return true;
}

void Gather_Init () {
	gather = TriggerCreate("Gather_Func");
	TriggerAddEventUnitAbility(gather, null, AbilityCommand("SCVHarvest", 0), c_abilHarvestStageHarvest, false);
}

bool Returning_Func (bool testConds, bool runActions) {
	if (!runActions) {
		return true;
	}
	if (UnitHasBehavior(EventUnitTargetUnit(), "Refinery") && UnitGetPropertyInt(EventUnit(), c_unitPropCarriedMinerals, true) > 0) {
		UnitIssueOrder(EventUnit(), OrderTargetingUnit(AbilityCommand("SCVHarvest", 0), EventUnitTargetUnit()), c_orderQueueAddToFront);
	}
	return true;
}

void Returning_Init () {
	returning = TriggerCreate("Returning_Func");
	TriggerAddEventUnitAbility(returning, null, AbilityCommand("SCVHarvest", 1), c_abilHarvestStageApproachDropOff, false);
}

bool Idle_Func (bool testConds, bool runActions) {
	int owner;
	unit idleUnit;
	unitgroup resourceGroup;
	bool remainsIdle;
	if (testConds) {
		if (UnitHasBehavior(EventUnit(), "IdleTaken")) {
			return false;
		}
		if (!UnitAbilityExists(EventUnit(), "SCVHarvest")) {
			return false;
		}
		if (PlayerType(UnitGetOwner(EventUnit())) == c_playerTypeComputer) {
			//return false;
		}
		if (UnitOrderCount(EventUnit()) > 0 && !UnitHasBehavior(OrderGetTargetUnit(UnitOrder(EventUnit(), 0)), "RefineryBounce")) {
			return false;
		}
	}
	
	if (!runActions) {
		return false;
	}
	idleUnit = EventUnit();
	owner = EventPlayer();
	if (UnitGetPropertyInt(idleUnit, c_unitPropCarriedMinerals, true) < 1400 && !UnitHasBehavior(OrderGetTargetUnit(UnitOrder(EventUnit(), 0)), "RefineryBounce")) {
		OrderWorkerToGather(idleUnit);
	} else {
		UnitIssueOrder(idleUnit, Order(AbilityCommand("SCVHarvest", 1)), c_orderQueueReplace);
		AISetUnitScriptControlled(idleUnit, false);
	}
	UnitBehaviorAdd(idleUnit, "IdleTaken", idleUnit, 1);
	do {
		remainsIdle = (UnitOrderCount(idleUnit) == 0);
		Wait(1, c_timeGame);
		if ((UnitOrderCount(idleUnit) == 0 && remainsIdle) || (UnitOrderCount(EventUnit()) > 0 && UnitHasBehavior(OrderGetTargetUnit(UnitOrder(idleUnit, 0)), "RefineryBounce"))) {
			resourceGroup = UnitGroup("RefineryBounce", owner, RegionEntireMap(), UnitFilter(0,0,0,0), 0);
			UnitIssueOrder(idleUnit, Order(AbilityCommand("stop", 0)), c_orderQueueReplace);
			UnitIssueOrder(idleUnit, OrderTargetingUnit(AbilityCommand("SCVHarvest", 0), AIGetClosestUnit(idleUnit, resourceGroup, true)), c_orderQueueAddToEnd);
			if (UnitHasBehavior(OrderGetTargetUnit(UnitOrder(idleUnit, 0)), "RefineryBounce")) {
				UnitIssueOrder(idleUnit, OrderTargetingUnit(AbilityCommand("SCVHarvest", 0), AIGetClosestUnit(idleUnit, UnitGroup("Refinery2", owner, RegionEntireMap(), UnitFilter(0,0,0,0), 0), true)), c_orderQueueAddToFront);
			}
		}
		AISetUnitScriptControlled(idleUnit, false);
	} while ((UnitOrderCount(idleUnit) == 0 || remainsIdle) || (UnitOrderCount(EventUnit()) > 0 && UnitHasBehavior(OrderGetTargetUnit(UnitOrder(idleUnit, 0)), "RefineryBounce")));
	UnitBehaviorRemove(idleUnit, "IdleTaken", 1);
	return true;
}

void Idle_Init () {
	idle = TriggerCreate("Idle_Func");
	TriggerAddEventUnitBecomesIdle(idle, null, true);
	TriggerAddEventUnitAbility(idle, null, AbilityCommand("SCVHarvest", 0), c_abilHarvestStageApproachResource, false);
}

bool Building_Func (bool testConds, bool runActions) {
	unit buildingUnit;
	fixed progress;
	int totaledCost;
	int prevCost;
	bool paused;
	fixed totalTime;
	
	if (testConds) {
		if (UnitQueueItemCount(EventUnit(), 1) == 0) {
			return false;
		}
		if (UnitHasBehavior(buildingUnit, "SupplyDepotStartingUpgrade")) {
			return false;
		}
	}
	
	buildingUnit = EventUnit();
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
	while (UnitHasBehavior(buildingUnit, "BuildHandled")) {
		Wait(0.0625, c_timeGame);
	}
	totalTime = UnitQueueItemTime(buildingUnit, c_unitQueueTimeTotal, 1);
	UnitBehaviorAdd(buildingUnit, "BuildHandled", buildingUnit, 1);
	do {
		if (UnitHasBehavior(buildingUnit, "SupplyDepotStartingUpgrade")) {
			return true;
		}
		if (!UnitIsAlive(buildingUnit)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if ((progress > UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) || UnitQueueItemCount(buildingUnit, 1) == 0) && UnitCheckProgressState(buildingUnit, 1, c_unitProgressStatePaused)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < FloorI(UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) * spendrate - totaledCost)) {
			UnitBehaviorAdd(buildingUnit, "ConstructStop", buildingUnit, 1);
			Wait(0.0625, c_timeGame);
			continue;
		}
		UnitBehaviorRemove(buildingUnit, "ConstructStop", 1);
		prevCost = totaledCost;
		totaledCost = FloorI(UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) * spendrate);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, totaledCost - prevCost);
		progress = UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1);
		paused = UnitCheckProgressState(buildingUnit, 1, c_unitProgressStatePaused);
		Wait(0.0625, c_timeGame);
	} while (progress <= UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1));
	if (!UnitIsAlive(buildingUnit)) {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		return true;
	}
	UnitBehaviorRemove(buildingUnit, "BuildHandled", 1);
	Wait(0.0625, c_timeGame);
	if (!(paused || UnitHasBehavior(buildingUnit, "Cancel"))) { // behavior appears to not work but it's here anyways because we can always hope
		while (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < (RoundI(totalTime * spendrate) - totaledCost)) {
			Wait(0.0625, c_timeGame);
		}
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, RoundI(totalTime * spendrate) - totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	} else {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	}
	return true;
}

void Building_Init () {
	building = TriggerCreate("Building_Func");
	TriggerAddEventUnitTrainProgress(building, null, c_unitProgressStageStart);
	TriggerAddEventUnitSpecializeProgress(building, null, c_unitProgressStageStart);
}

bool Techlab_Func (bool testConds, bool runActions) {
	unit buildingUnit;
	fixed progress;
	int totaledCost;
	int prevCost;
	bool paused;
	fixed totalTime;
	
	if (testConds) {
		if (libNtve_gf_TriggeringProgressUnitType() == null) {
			return false;
		}
		if (!(AIDefaultGetObjectType(UnitGetOwner(EventUnit()), libNtve_gf_TriggeringProgressUnitType()) == 1 && StringContains(libNtve_gf_TriggeringProgressUnitType(), "TechLab", c_stringAnywhere, c_stringNoCase))) {
			return false;
		}
	}
	
	buildingUnit = EventUnit();
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	while (UnitHasBehavior(buildingUnit, "BuildHandled")) {
		Wait(0.0625, c_timeGame);
	}
	totalTime = 2;
	UnitBehaviorAdd(buildingUnit, "BuildHandled", buildingUnit, 1);
	do {
		if (!UnitIsAlive(buildingUnit)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if (progress > UnitOrderGetProgress(buildingUnit)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < FloorI(UnitOrderGetProgress(buildingUnit)/100 * totalTime * spendrate - totaledCost)) {
			UnitBehaviorAdd(buildingUnit, "ConstructStop", buildingUnit, 1);
			Wait(0.0625, c_timeGame);
			continue;
		}
		UnitBehaviorRemove(buildingUnit, "ConstructStop", 1);
		prevCost = totaledCost;
		totaledCost = FloorI(UnitOrderGetProgress(buildingUnit)/100 * totalTime * spendrate);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, totaledCost - prevCost);
		progress = UnitOrderGetProgress(buildingUnit);
		paused = UnitCheckProgressState(buildingUnit, 1, c_unitProgressStatePaused);
		Wait(0.0625, c_timeGame);
	} while (progress <= UnitOrderGetProgress(buildingUnit) && progress != 100);
	if (!UnitIsAlive(buildingUnit)) {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		return true;
	}
	UnitBehaviorRemove(buildingUnit, "BuildHandled", 1);
	Wait(0.0625, c_timeGame);
	if (!(paused || UnitHasBehavior(buildingUnit, "Cancel"))) { // behavior appears to not work but it's here anyways because we can always hope
		while (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < (RoundI(totalTime * spendrate) - totaledCost)) {
			Wait(0.0625, c_timeGame);
		}
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, RoundI(totalTime * spendrate) - totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	} else {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	}
	return true;
}

void Techlab_Init () {
	techlab = TriggerCreate("Techlab_Func");
	TriggerAddEventUnitConstructProgress(techlab, null, c_unitProgressStageStart);
}

bool Research_Func (bool testConds, bool runActions) {
	unit buildingUnit;
	fixed progress;
	int targetCost;
	fixed costRate;
	int totaledCost;
	int prevCost;
	bool paused;
	fixed totalTime;
	
	if (testConds) {
		if (UnitQueueItemCount(EventUnit(), 1) == 0) {
			return false;
		}
	}
	
	buildingUnit = EventUnit();
	while (UnitHasBehavior(buildingUnit, "BuildHandled")) {
		Wait(0.0625, c_timeGame);
	}
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
	PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	targetCost = CatalogFieldValueGetAsInt(c_gameCatalogUpgrade, libNtve_gf_TriggeringProgressUpgrade(), "BonusResourcePerLevel[0]", c_playerAny);
	totalTime = UnitQueueItemTime(buildingUnit, c_unitQueueTimeTotal, 1);
	costRate = Round(IntToFixed(targetCost) / totalTime);
	UnitBehaviorAdd(buildingUnit, "BuildHandled", buildingUnit, 1);
	do {
		if (!UnitIsAlive(buildingUnit)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if ((progress > UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) || UnitQueueItemCount(buildingUnit, 1) == 0) && UnitCheckProgressState(buildingUnit, 1, c_unitProgressStatePaused)) {
			PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
			return true;
		}
		if (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < FloorI(UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) * costRate - totaledCost)) {
			UnitBehaviorAdd(buildingUnit, "ConstructStop", buildingUnit, 1);
			Wait(0.0625, c_timeGame);
			continue;
		}
		UnitBehaviorRemove(buildingUnit, "ConstructStop", 1);
		prevCost = totaledCost;
		totaledCost = FloorI(UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1) * costRate);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, totaledCost - prevCost);
		progress = UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1);
		paused = UnitCheckProgressState(buildingUnit, 1, c_unitProgressStatePaused);
		Wait(0.0625, c_timeGame);
	} while (progress <= UnitQueueItemTime(buildingUnit, c_unitQueueTimeElapsed, 1));
	if (!UnitIsAlive(buildingUnit)) {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		return true;
	}
	UnitBehaviorRemove(buildingUnit, "BuildHandled", 1);
	Wait(0.0625, c_timeGame);
	if (!(paused || UnitHasBehavior(buildingUnit, "Cancel"))) { // behavior appears to not work but it's here anyways because we can always hope
		while (PlayerGetPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals) < (RoundI(totalTime * costRate) - totaledCost)) {
			Wait(0.0625, c_timeGame);
		}
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperSubtract, targetCost - totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	} else {
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropMinerals, c_playerPropOperAdd, totaledCost);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropCustom, c_playerPropOperSetTo, 2000000);
		PlayerModifyPropertyInt(UnitGetOwner(buildingUnit), c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	}
	return true;
}

void Research_Init () {
	research = TriggerCreate("Research_Func");
	TriggerAddEventUnitResearchProgress(research, null, c_unitProgressStageStart);
}

bool Pause_Func (bool testConds, bool runActions) {
	if (UnitQueueItemCount(EventUnit(), 1) != 0) {
		UnitSetProgressStage(EventUnit(), 1, c_unitProgressStagePause);
	}
	return true;
}

void Pause_Init () {
	pause = TriggerCreate("Pause_Func");
	TriggerAddEventUnitAbility(pause, null, AbilityCommand("PauseResumeQueue", 0), c_unitAbilStageAll, false);
}

bool Resume_Func (bool testConds, bool runActions) {
	UnitSetProgressStage(EventUnit(), 1, c_unitProgressStageResume);
	return true;
}

void Resume_Init () {
	resume = TriggerCreate("Resume_Func");
	TriggerAddEventUnitAbility(resume, null, AbilityCommand("PauseResumeQueue", 1), c_unitAbilStageAll, false);
}

bool Cancel_Func (bool testConds, bool runActions) {
	if (UnitQueueItemCount(EventUnit(), 1) == 0) {
		UnitBehaviorRemove(EventUnit(), "QueuePaused", 1);
	}
	if (UnitHasBehavior(EventUnit(), "QueuePaused")) {
	UnitSetProgressStage(EventUnit(), 1, c_unitProgressStagePause);
	}
	UnitBehaviorAdd(EventUnit(), "Cancel", EventUnit(), 1);
	return true;
}

void Cancel_Init () {
	cancel = TriggerCreate("Cancel_Func");
	TriggerAddEventUnitTrainProgress(cancel, null, c_unitProgressStageCancel);
	TriggerAddEventUnitSpecializeProgress(cancel, null, c_unitProgressStageCancel);
}

bool UnloadSquads_Func (bool testConds, bool runActions) {
	order ord;
//	if (UnitGetMagazine(EventUnitCargo()) != null) {
//		if (libNtve_gf_UnitIsInsideTransport(UnitGetMagazine(EventUnitCargo()))) {
//			UnitPutInTransport(EventUnitCargo(), EventUnit());
//		}
//	}
	ord = Order(AbilityCommand("AirTransport", 3));
	OrderSetTargetPassenger(ord, EventPlayerEffectUsedUnit(c_effectUnitTarget));
	UnitIssueOrder(UnitTransport(EventPlayerEffectUsedUnit(c_effectUnitTarget)), ord, c_orderQueueAddToFront);
	return true;
}

void UnloadSquads_Init () {
	unloadSquads = TriggerCreate("UnloadSquads_Func");
	//TriggerAddEventUnitCargo(unloadSquads, null, false);
	TriggerAddEventPlayerEffectUsed(unloadSquads, c_playerAny, "AirTransportUnloadDummy");
}

bool Power_Func (bool testConds, bool runActions) {
	fixed rate;
	if (PlayerGetPropertyInt(EventPlayer(), c_playerPropSuppliesUsed) == 0) {
		return false;
	}
	rate = MinF(PlayerGetPropertyFixed(EventPlayer(), c_playerPropSuppliesMade) / PlayerGetPropertyFixed(EventPlayer(), c_playerPropSuppliesUsed), 1);
	if (rate == 0) {
		rate = 1;
	}
	libNtve_gf_CatalogFieldValueSetAsReal(c_gameCatalogBehavior, "PowerCheck", "Modification.RateMultiplierArray[3]", EventPlayer(), rate);
	libNtve_gf_CatalogFieldValueSetAsReal(c_gameCatalogBehavior, "PowerCheck", "Modification.RateMultiplierArray[5]", EventPlayer(), rate);
	libNtve_gf_CatalogFieldValueSetAsReal(c_gameCatalogBehavior, "PowerCheck", "Modification.MoveSpeedMultiplier", EventPlayer(), rate);
	if (rate < 1) {
		libNtve_gf_SetUpgradeLevelForPlayer(EventPlayer(), "LowPower", 1);
	} else {
		libNtve_gf_SetUpgradeLevelForPlayer(EventPlayer(), "LowPower", 0);
	}
	return true;
}

void Power_Init () {
	power = TriggerCreate("Power_Func");
	TriggerAddEventPlayerPropChange(power, c_playerAny, c_playerPropSuppliesUsed);
	TriggerAddEventPlayerPropChange(power, c_playerAny, c_playerPropSuppliesMade);
}

bool OwnerSwap_Func (bool testConds, bool runActions) {
	if (UnitHasBehavior(EventUnit(), "PowerCheck")) {
		UnitBehaviorRemove(EventUnit(), "PowerCheck", 1);
		UnitBehaviorAddPlayer(EventUnit(), "PowerCheck", EventUnitOwnerNew(), 1);
	}
	if (EventUnitOwnerNew() == 1) {
		AddTopbars();
		buildingSwap(EventUnit());
	}
	return true;
}

void OwnerSwap_Init () {
	ownerSwap = TriggerCreate("OwnerSwap_Func");
	TriggerAddEventUnitChangeOwner(ownerSwap, null);
}

bool TopbarCheck_Func (bool testConds, bool runActions) {
	RefreshTopbar();
	return true;
}

void TopbarCheck_Init () {
	topbarCheck = TriggerCreate("TopbarCheck_Func");
	TriggerAddEventUnitConstructProgress(topbarCheck, null, c_unitProgressStageComplete);
	TriggerAddEventUnitDied(topbarCheck, null);
	TriggerAddEventUnitBehaviorChange(topbarCheck, null, "Depowered", c_unitBehaviorChangeAny);
}

bool RallyCopy_Func (bool testConds, bool runActions) {
	UnitCreateEffectUnit(EventUnit(), "RallyIterate", EventUnit());
	return true;
}

void RallyCopy_Init () {
	rallyCopy = TriggerCreate("RallyCopy_Func");
	TriggerAddEventUnitAbility(rallyCopy, null, AbilityCommand("Rally", 0), c_unitAbilStageAll, true);
}

bool TechUnlock_Func (bool textConds, bool runActions) {
	int lv_i;
	unitgroup swaps;
	unit currentLoop;
	swaps = UnitGroup(null, 1, RegionEntireMap(), UnitFilterStr("-;Dead,Missile,Hidden"), 0);
	
	for (lv_i = UnitGroupCount(swaps, c_unitCountAlive);; lv_i -= 1) {
		currentLoop = UnitGroupUnitFromEnd(swaps, lv_i);
		if (currentLoop == null) { break; }
		buildingSwap(currentLoop);
	}
	if (noTopBar) {
		DialogControlSetVisible(topbarCastingPanel, PlayerGroupAll(), false);
		DialogControlSetVisible(topbarPanel, PlayerGroupAll(), false);
		DialogControlSetVisible(topbarCommandPanel, PlayerGroupAll(), false);
	} else {
		AddTopbars();
	}
	PlayerModifyPropertyInt(1, c_playerPropMinerals, c_playerPropOperSetTo, 3000);
	PlayerModifyPropertyInt(1, c_playerPropCustom, c_playerPropOperSetTo, 2000000);
	PlayerModifyPropertyInt(1, c_playerPropVespene, c_playerPropOperSetTo, 2000000);
	TechTreeAbilityAllow(1, AbilityCommand("SquadStimpack", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("Stimpack", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("MakeVultureSpiderMines", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("330mmBarrageCannons", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("EMPSpectre", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("TrainStone", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 1), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 2), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 3), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 8), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 0), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 1), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 2), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 3), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 8), true);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 7), ConversationDataStateGetValue("MissionCompletedCount") >= 3);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 7), ConversationDataStateGetValue("MissionCompletedCount") >= 3);
    TechTreeAbilityAllow(1, AbilityCommand("Barracks2AddOns", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksAddOns", 0)));
    TechTreeAbilityAllow(1, AbilityCommand("Factory2AddOns", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryAddOns", 0)) || TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryTrain", 5)) || ConversationDataStateGetValue("MissionCompletedCount") >= 3); //hellion
    TechTreeAbilityAllow(1, AbilityCommand("Starport2AddOns", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("StarportAddOns", 0)) || TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 0)) || TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 4)) || ConversationDataStateGetValue("MissionCompletedCount") >= 6);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 10), (TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 3)) || TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 5))));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 10), (TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 3)) || TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 5))));
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 4), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 6)));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 4), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 6)));
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 5), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 5), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 6), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 8)));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 6), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 8)));
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 13), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 13), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 9), ConversationDataStateGetValue("MissionCompletedCount") >= 6);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 9), ConversationDataStateGetValue("MissionCompletedCount") >= 6);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 11), ConversationDataStateGetValue("MissionCompletedCount") >= 9);
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 11), ConversationDataStateGetValue("MissionCompletedCount") >= 9);
	TechTreeAbilityAllow(1, AbilityCommand("CCBuild", 14), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 19)));
	TechTreeAbilityAllow(1, AbilityCommand("CCPlaceBuilding", 14), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 19)));
	TechTreeAbilityAllow(1, AbilityCommand("EngineeringBay2Research", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("EngineeringBay2Research", 1), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("BarracksTechLabResearch", 3), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 0)));
	TechTreeAbilityAllow(1, AbilityCommand("BarracksTechLabResearch", 4), TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 5)));
	TechTreeAbilityAllow(1, AbilityCommand("GhostAcademy2Research", 2), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 3)));
    TechTreeAbilityAllow(1, AbilityCommand("UpgradeToPlanetaryFortress2", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("UpgradeToPlanetaryFortress", 0)));
    TechTreeAbilityAllow(1, AbilityCommand("Armory2Research", 0), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 5)) || TechTreeAbilityIsAllowed(1, AbilityCommand("TerranBuild", 19)));
    TechTreeAbilityAllow(1, AbilityCommand("Armory2Research", 1), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", 3)) || TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryTrain", 6)));
    TechTreeAbilityAllow(1, AbilityCommand("Armory2Research", 2), TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryTrain", 7)) || TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryTrain", 6)) || TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 4)));
    TechTreeAbilityAllow(1, AbilityCommand("Armory2Research", 3), TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 7)) || TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 3)) || TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", 3)));
	for (lv_i = 0; lv_i < 9; lv_i += 1) {
		TechTreeAbilityAllow(1, AbilityCommand("Barracks2Train", lv_i), TechTreeAbilityIsAllowed(1, AbilityCommand("BarracksTrain", lv_i)));
	}
	for (lv_i = 0; lv_i < 10; lv_i += 1) {
		TechTreeAbilityAllow(1, AbilityCommand("Factory2Train", lv_i), TechTreeAbilityIsAllowed(1, AbilityCommand("FactoryTrain", lv_i)));
	}
	TechTreeAbilityAllow(1, AbilityCommand("Factory2Train", 9), ConversationDataStateGetValue("MissionCompletedCount") >= 3);
	for (lv_i = 0; lv_i < 8; lv_i += 1) {
		TechTreeAbilityAllow(1, AbilityCommand("Starport2Train", lv_i), TechTreeAbilityIsAllowed(1, AbilityCommand("StarportTrain", lv_i)));
	}
	TechTreeAbilityAllow(1, AbilityCommand("Starport2Train", 5), ConversationDataStateGetValue("MissionCompletedCount") >= 6);
	if (TechTreeUpgradeCount(1, "UltraCapacitors", c_techCountCompleteOnly) > 0) {
		TechTreeUpgradeAddLevel(1, "TerranInfantryUltraCapacitors", 1);
		TechTreeUpgradeAddLevel(1, "TerranVehicleUltraCapacitors", 1);
		TechTreeUpgradeAddLevel(1, "TerranShipUltraCapacitors", 1);
	}
	if (TechTreeUpgradeCount(1, "VanadiumPlating", c_techCountCompleteOnly) > 0) {
		TechTreeUpgradeAddLevel(1, "TerranInfantryVanadiumPlating", 1);
		TechTreeUpgradeAddLevel(1, "TerranVehicleVanadiumPlating", 1);
		TechTreeUpgradeAddLevel(1, "TerranShipVanadiumPlating", 1);
	}
	if (TechTreeUpgradeCount(1, "SupplyDepotDrop", c_techCountCompleteOnly) > 0) {
		UnitBehaviorAddPlayer(topbarUnit, "OrbitalSupplyDrop", 1, 1);
	}
	if (TechTreeUpgradeCount(1, "MicroFiltering", c_techCountCompleteOnly) > 0) {
		rate += 116;
	}
	TechTreeAbilityAllow(1, ConversationDataStateAbilCmd("CCEditionPermanence|Laser_Drill", ("AC1")), ConversationDataStateGetValue("CCEditionPermanence|Laser_Drill") > 0);
	TechTreeAbilityAllow(1, AbilityCommand("ZergHypervirus", 0), ConversationDataStateGetValue(ConversationDataStateIndex("MissionCompleted", 6)) > 0);
	TechTreeAbilityAllow(1, AbilityCommand("CallInGoldenArmada", 0), ConversationDataStateGetValue(ConversationDataStateIndex("MissionCompleted", 7)) > 0);
	ConversationDataStateSetValue("StoryState|AchievementDisabled", 1);
	AchievementsDisable(1); //one of these should work
	return true;
}

void TechUnlock_Init () {
	techUnlock = TriggerCreate("TechUnlock_Func");
	TriggerAddEventTimeElapsed(techUnlock, 0.125, c_timeGame);
}

bool INI_Func (bool testConds, bool runActions) {
	int lv_lastFrame;
	int panel;
	
	UnitCreate(1, "CCTopbar", 0, 1, PlayerStartLocation(1), 0);
	topbarUnit = UnitLastCreated();
	DialogControlHookupStandard(c_triggerControlTypePanel, "UIContainer/FullscreenUpperContainer/CCGlobalCastingPanel");
	topbarCastingPanel = DialogControlLastCreated();
	DialogControlSetVisible(DialogControlLastCreated(), PlayerGroupAll(), true);
	DialogControlCreateInPanelFromTemplate(DialogControlLastCreated(), c_triggerControlTypePanel, "CCTopbar/CCPanelTemplate");
	topbarPanel = DialogControlLastCreated();
	DialogControlSetVisible(topbarPanel, PlayerGroupAll(), true);
	DialogControlCreateInPanel(DialogControlLastCreated(), c_triggerControlTypeTooltip);
	DialogControlSetSize(DialogControlLastCreated(), PlayerGroupAll(), 1, 1);
	DialogControlSetPosition(DialogControlLastCreated(), PlayerGroupAll(), c_anchorTopLeft, -100, -100);
	DialogControlHookupStandard(c_triggerControlTypePanel, "UIContainer/ConsolePanel");
	lv_lastFrame = DialogControlLastCreated();
	DialogControlHookup(topbarPanel, c_triggerControlTypePanel, "CCButtonHolder");
	lv_lastFrame = DialogControlLastCreated();
	DialogControlCreateInPanelFromTemplate(lv_lastFrame, c_triggerControlTypeCommandPanel, "CCTopbar/CCCommandPanelTemplate");
	lv_lastFrame = DialogControlLastCreated();
	//DialogControlHookup(lv_lastFrame, c_triggerControlTypeButton, "CommandButton00");
	//DialogControlHookup(lv_lastFrame, c_triggerControlTypeButton, "CommandButton01");
	//DialogControlHookup(lv_lastFrame, c_triggerControlTypeButton, "CommandButton02");
	libNtve_gf_SetDialogItemUnitGroup(lv_lastFrame, libNtve_gf_ConvertUnitToUnitGroup(topbarUnit), PlayerGroupAll());
	topbarCommandPanel = DialogControlLastCreated();
	UISetResourceVisible(PlayerGroupAll(), c_resourceTypeVespene, false);
	rate = 117;
	spendrate = 100;
	//PlayerSetState(1, c_playerStateFormationMove, true);
	BankLoad("TCampaign", 1);
	ConversationDataLoadStateValues("CCEditionPermanence", BankLastCreated(), "CCEditionPermanence");
	PlayerModifyPropertyInt(1, c_playerPropSuppliesLimit, c_playerPropOperSetTo, 10000);
	return true;
}

void INI_Init () {
	INI = TriggerCreate("INI_Func");
	TriggerAddEventMapInit(INI);
}

void main() {
	Gather_Init();
	Returning_Init();
	Idle_Init();
	Building_Init();
	Techlab_Init();
	Research_Init();
	Pause_Init();
	Resume_Init();
	Cancel_Init();
	UnloadSquads_Init();
	Power_Init();
	OwnerSwap_Init();
	TopbarCheck_Init();
	RallyCopy_Init();
	TechUnlock_Init();
	INI_Init();
}